group 'spring.basic.api'
version '1.0-SNAPSHOT'

buildscript {

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.2.2.RELEASE")
    }

}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11


bootRun {
    systemProperties = System.properties
    main = 'com.sundeepk.spring.basic.api.Application'
}

task runMain(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = 'com.sundeepk.spring.basic.api.Application'
}


task acceptanceTest(type: Exec, dependsOn: ["test"]) { thisTask ->
    group = "verification"
    description = "Runs the acceptance tests"

    commandLine "test/cucumber/test.sh"

    check.dependsOn(thisTask)
    thisTask.mustRunAfter(test)
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework:spring-web")
    compile("com.fasterxml.jackson.core:jackson-databind")

    testCompile("com.jayway.jsonpath:json-path:2.2.0")
    testCompile("org.springframework.boot:spring-boot-starter-test")

    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    testCompile('org.mockito:mockito-junit-jupiter:2.23.0')

}
